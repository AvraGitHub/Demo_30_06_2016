*&---------------------------------------------------------------------*
*& Report  ZDTP_MASSDOWNLOAD_AVRA
*&
*&---------------------------------------------------------------------*
*&
*&
*&---------------------------------------------------------------------*

REPORT  ZDTP_MASSDOWNLOAD_AVRA.

*----------------------------------------------------------------------------------------------------------------------
*  SAP Tables
*----------------------------------------------------------------------------------------------------------------------
tables: trdir, seoclass, tfdir, enlfdir, dd02l, tadiv.
type-pools: abap, seor.

*----------------------------------------------------------------------------------------------------------------------
*  Types
*----------------------------------------------------------------------------------------------------------------------
* text element structure
types: tTextTable like textpool.
* GUI titles
types: tGUITitle like d347t.

* Message classes
types: begin of tMessage,
         arbgb like t100-arbgb,
         stext like t100a-stext,
         msgnr like t100-msgnr,
         text  like t100-text,
       end of tMessage.

* Screen flow.
types: begin of tScreenFlow,
         screen like d020s-dnum,
         code like d022s-line,
       end of tScreenFlow.

* Holds a tabledefinition
types: begin of tDictTableStructure,
         fieldname like dd03l-fieldname,
         position  like dd03l-position,
         keyflag   like dd03l-keyflag,
         rollname  like dd03l-rollname,
         domname   like dd03l-domname,
         datatype  like dd03l-datatype,
         leng      like dd03l-leng,
         lowercase type lowercase,
         ddtext    like dd04t-ddtext,
       end of tdictTableStructure.

* Holds a tables attributes + its definition
types: begin of tDictTable,
         tablename    like dd03l-tabname,
         tableTitle   like dd02t-ddtext,
         iStructure type tDictTableStructure occurs 0,
       end of tDictTable.

types: begin of tDictFilename,
         tablename    like dd03l-tabname,
         filename type string,
       end of tDictFilename.

* Include program names
types: begin of tInclude,
         includeName like trdir-name,
         includeTitle like tftit-stext,
       end of tInclude.

* Exception class texts
types: begin of tConcept,
         constName type string,
         concept type sotr_conc,
       end of tConcept.

* Method
types: begin of tMethod,
         cmpName(61),
         descript like vseomethod-descript,
         exposure like vseomethod-exposure,
         methodKey type string,
       end of tMethod.

* Interfaces
types: begin of tInterface,
         interfaceName like vseoclass-clsname,
       end of tInterface.

* Class
types: begin of tClass,
         scanned(1),
         clsname like vseoclass-clsname,
         descript like vseoclass-descript,
         msg_id like vseoclass-msg_id,
         exposure like vseoclass-exposure,
         state like vseoclass-state,
         clsfinal like vseoclass-clsfinal,
         r3release like vseoclass-r3release,
         iMethods type tMethod occurs 0,
         iDictStruct type tDictTable occurs 0,
         iTextElements type tTextTable occurs 0,
         iMessages type tMessage occurs 0,
         iInterfaces type tInterface occurs 0,
         iConcepts type tConcept occurs 0,
         textElementKey type string,
         publicClassKey type string,
         privateClassKey type string,
         protectedClassKey type string,
         typesClassKey type string,
         exceptionClass type abap_bool,
       end of tClass.

* function modules
types: begin of tFunction,
         functionName like tfdir-funcName,
         functionGroup like enlfdir-area,
         includeNumber like tfdir-include,
         functionMainInclude like tfdir-funcName,
         functionTitle like tftit-stext,
         topIncludeName like tfdir-funcName,
         progname like tfdir-pname,
         programLinkName like tfdir-pname,
         messageClass like t100-arbgb,
         iTextElements type tTextTable occurs 0,
         iSelectiontexts type tTextTable occurs 0,
         iMessages type tMessage occurs 0,
         iIncludes type tInclude occurs 0,
         iDictStruct type tDictTable occurs 0,
         iGUITitle type tGUITitle occurs 0,
         iScreenFlow type tScreenFlow occurs 0,
       end of tFunction.

types: begin of tProgram,
         progname like trdir-name,
         programTitle like tftit-stext,
         subc like trdir-subc,
         messageClass like t100-arbgb,
         iMessages type tMessage occurs 0,
         iTextElements type tTextTable occurs 0,
         iSelectiontexts type tTextTable occurs 0,
         iGUITitle type tGUITitle occurs 0,
         iScreenFlow type tScreenFlow occurs 0,
         iIncludes type tInclude occurs 0,
         iDictStruct type tDictTable occurs 0,
       end of tProgram.

*----------------------------------------------------------------------------------------------------------------------
*  Internal tables
*----------------------------------------------------------------------------------------------------------------------
*  Dictionary object
data: iDictionary type standard table of tDictTable with header line.
*  Dictionary objects which have previously been downloaded
data: iDictFilename type standard table of tDictFilename with header line.
* Function modules.
data: iFunctions type standard table of tFunction with header line.
* Function modules used within programs.
data: iProgFunctions type standard table of tFunction with header line.
* Tree display structure.
data: iTreeDisplay type standard table of snodetext with header line.
* Message class data
data: iMessages type standard table of tMessage with header line.
* Holds a single message class an all of its messages
data: iSingleMessageClass type standard table of tMessage with header line.
* Holds program related data
data: iPrograms type standard table of tProgram with header line.
* Classes
data: iClasses type standard table of tClass with header line.
* Table of paths created on the SAP server
data: iServerPaths type standard table of string with header line.

*----------------------------------------------------------------------------------------------------------------------
*  Table prototypes
*----------------------------------------------------------------------------------------------------------------------
data: dumiDictStructure type standard table of tDictTableStructure.
data: dumiTextTab type standard table of tTextTable.
data: dumiIncludes type standard table of tInclude.
data: dumiHtml type standard table of string.
data: dumiHeader type standard table of string .
data: dumiScreen type standard table of tScreenFlow .
data: dumIGUITitle type standard table of tGUITitle.
data: dumiMethods type standard table of tMethod.
data: dumiConcepts type standard table of tConcept.
data: dumiInterfaces type standard table of tInterface.

*----------------------------------------------------------------------------------------------------------------------
*   Global objects
*----------------------------------------------------------------------------------------------------------------------
data: objFile type ref to cl_gui_frontend_services.
data: objRuntimeError type ref to cx_root.

*----------------------------------------------------------------------------------------------------------------------
*  Constants
*----------------------------------------------------------------------------------------------------------------------
constants: VERSIONNO type string value '1.4.4'.
constants: TABLES type string value 'TABLES'.
constants: TABLE type string value 'TABLE'.
constants: LIKE type string value 'LIKE'.
constants: TYPE type string value 'TYPE'.
constants: TYPEREFTO type string value 'TYPE REF TO'.
constants: STRUCTURE type string value 'STRUCTURE'.
constants: LOWSTRUCTURE type string value 'structure'.
constants: OCCURS type string value 'OCCURS'.
constants: FUNCTION type string value 'FUNCTION'.
constants: CALLFUNCTION type string value ' CALL FUNCTION'.
constants: MESSAGE type string  value 'MESSAGE'.
constants: INCLUDE type string value 'INCLUDE'.
constants: LOWINCLUDE type string value 'include'.
constants: DESTINATION type string value 'DESTINATION'.
constants: IS_TABLE type string value 'T'.
constants: IS_PROGRAM type string value 'P'.
constants: IS_SCREEN type string value 'S'.
constants: IS_GUITITLE type string value 'G'.
constants: IS_DOCUMENTATION type string value 'D'.
constants: IS_MESSAGECLASS type string value 'MC'.
constants: IS_FUNCTION type string value 'F'.
constants: IS_CLASS type string value 'C'.
constants: IS_METHOD type string value 'M'.
constants: ASTERIX type string value '*'.
constants: COMMA type string value ','.
constants: PERIOD type string value '.'.
constants: DASH type string value '-'.
constants: TRUE type abap_bool value 'X'.
constants: FALSE type abap_bool value ''.
constants: LT type string value '&lt;'.
constants: GT type string value '&gt;'.
constants: UNIX type string value 'UNIX'.
constants: NON_UNIX type string value 'not UNIX'.
constants: HTMLEXTENSION type string value 'html'.
constants: TEXTEXTENSION type string value 'txt'.
constants: SS_CODE type c value 'C'.
constants: SS_TABLE type c value 'T'.

*----------------------------------------------------------------------------------------------------------------------
*  Global variables
*----------------------------------------------------------------------------------------------------------------------
data: statusBarMessage(100).
data: forcedExit type abap_bool value FALSE.
data: startTime like sy-uzeit.
data: runTime like sy-uzeit.
data: downloadFileExtension type string.
data: downloadFolder type string.
data: serverSlashSeparator type string.
data: frontendSlashSeparator type string.
data: slashSeparatorToUse type string.
data: serverFilesystem type filesys_d.
data: serverFolder type string.
data: frontendOpSystem type string.
data: serverOpSystem type string.
data: customerNameSpace type string.
ranges: soProgramName for trdir-name.
ranges: soAuthor for usr02-bname.
ranges: soTableNames for dd02l-tabname.
ranges: soFunctionName  for tfdir-funcName.
ranges: soClassName for vseoclass-clsname.
ranges: soFunctionGroup for enlfdir-area.
field-symbols: <waDictStruct> type tDictTable.

*----------------------------------------------------------------------------------------------------------------------
*  Selection screen declaration
*----------------------------------------------------------------------------------------------------------------------
* Author
selection-screen: begin of block b1 with frame title tBlock1.
  selection-screen begin of line.
    selection-screen comment 5(23) tAuth.
    parameters: pAuth like usr02-bname memory id MAUTH.
  selection-screen end of line.

  selection-screen begin of line.
    selection-screen comment 5(36) tPmod.
    parameters: pMod as checkbox.
  selection-screen end of line.
selection-screen: end of block b1.

selection-screen begin of block b2 with frame title tBlock2.
* Tables
  selection-screen begin of line.
    parameters: rTable radiobutton group r1.
    selection-screen comment 5(15) tRtable.
  selection-screen end of line.

  selection-screen begin of line.
    selection-screen comment 10(15) tPtable.
    select-options: soTable for dd02l-tabname.
  selection-screen end of line.

  selection-screen begin of line.
    selection-screen comment 10(79) tTnote.
  selection-screen end of line.

* Message classes
  selection-screen begin of line.
    parameters: rMess radiobutton group r1.
    selection-screen comment 5(18) tPMes.
  selection-screen end of line.

  selection-screen begin of line.
    selection-screen comment 10(18) tMname.
    parameters: pMname like t100-arbgb memory id MMNAME.
  selection-screen end of line.

* Function modules
  selection-screen begin of line.
    parameters: rFunc radiobutton group r1.
    selection-screen comment 5(30) tRfunc.
  selection-screen end of line.

  selection-screen begin of line.
    selection-screen comment 10(15) tPfname.
    select-options: soFname for tfdir-funcName.
  selection-screen end of line.

  selection-screen begin of line.
    selection-screen comment 10(15) tFgroup.
    select-options: soFgroup for enlfdir-area.
  selection-screen end of line.

* Classes
  selection-screen begin of line.
    parameters: rClass radiobutton group r1.
    selection-screen comment 5(30) tRClass.
  selection-screen end of line.

  selection-screen begin of line.
    selection-screen comment 10(15) tPcName.
    select-options: soClass for seoclass-clsname.
  selection-screen end of line.

* Programs / includes
  selection-screen begin of line.
    parameters: rProg radiobutton group r1 default 'X'.
    selection-screen comment 5(18) tProg.
  selection-screen end of line.

  selection-screen begin of line.
    selection-screen comment 10(15) tRpname.
    select-options: soProg for trdir-name.
  selection-screen end of line.

  selection-screen skip.
* Language
  selection-screen begin of line.
    selection-screen comment 1(27) tMLang.
    parameters: pMLang like t100-sprsl default 'EN'.
  selection-screen end of line.

* Package
  selection-screen begin of line.
    selection-screen comment 1(24) tPack.
    select-options: soPack for tadiv-devclass.
  selection-screen end of line.


* Customer objects
  selection-screen begin of line.
    selection-screen comment 1(27) tCust.
    parameters: pCust as checkbox default 'X'.
  selection-screen end of line.

* Alt customer name range
  selection-screen begin of line.
    selection-screen comment 1(27) tNRange.
    parameters: pCName type namespace memory id MNAMESPACE.
  selection-screen end of line.
selection-screen: end of block b2.

* Additional things to download.
selection-screen: begin of block b3 with frame title tBlock3.
  selection-screen begin of line.
    selection-screen comment 1(33) tPtext.
    parameters: pText as checkbox default 'X' memory id MTEXT.
  selection-screen end of line.

  selection-screen begin of line.
    selection-screen comment 1(33) tMess.
    parameters: pMess as checkbox default 'X' memory id MMESS.
  selection-screen end of line.

  selection-screen begin of line.
    selection-screen comment 1(33) tPinc.
    parameters: pInc as checkbox default 'X' memory id MINC.
    selection-screen comment 40(20) tReci.
    parameters: pReci as checkbox default 'X' memory id MRECI.
  selection-screen end of line.

  selection-screen begin of line.
    selection-screen comment 1(33) tPfunc.
    parameters: pFunc as checkbox default 'X' memory id MFUNC.
    selection-screen comment 40(20) tRecf.
    parameters: pRecf as checkbox default 'X' memory id MRECF.
  selection-screen end of line.

  selection-screen begin of line.
    selection-screen comment 1(33) tRecC.
    parameters: pRecC as checkbox default 'X' memory id MRECC.
  selection-screen end of line.

  selection-screen begin of line.
    selection-screen comment 1(33) tFDoc.
    parameters: pFDoc as checkbox default 'X' memory id MFDOC.
  selection-screen end of line.

  selection-screen begin of line.
    selection-screen comment 1(33) tCDoc.
    parameters: pCDoc as checkbox default 'X' memory id MCDOC.
  selection-screen end of line.

  selection-screen begin of line.
    selection-screen comment 1(33) tPscr.
    parameters: pScr as checkbox default 'X' memory id MSCR.
  selection-screen end of line.

  selection-screen begin of line.
    selection-screen comment 1(33) tPdict.
    parameters: pDict as checkbox default 'X' memory id MDICT.
  selection-screen end of line.

  selection-screen begin of line.
    selection-screen comment 1(33) tSortT.
    parameters: pSortT as checkbox default ' ' memory id MSORTT.
  selection-screen end of line.
selection-screen: end of block b3.

* File details
selection-screen: begin of block b4 with frame title tBlock4.
  selection-screen begin of line.
    selection-screen comment 1(20) tPhtml.
    parameters: pHtml radiobutton group g1 default 'X'.
  selection-screen end of line.

  selection-screen begin of line.
    selection-screen comment 5(29) tBack.
    parameters: pBack as checkbox default 'X'.
  selection-screen end of line.

  selection-screen begin of line.
    selection-screen comment 1(20) tPtxt.
    parameters: pTxt radiobutton group g1.
  selection-screen end of line.

  selection-screen skip.

* Download to SAP server
  selection-screen begin of line.
    selection-screen comment 1(25) tServ.
    parameters: pServ radiobutton group g2.
  selection-screen end of line.
  selection-screen begin of line.
    selection-screen comment 8(20) tSPath.
    parameters: pLogical like filename-fileintern memory id MLOGICAL.
  selection-screen end of line.
  selection-screen comment /28(60) tSDPath.

* Download to PC
  selection-screen begin of line.
    selection-screen comment 1(25) tPc.
    parameters: pPc radiobutton group g2 default 'X'.
  selection-screen end of line.
  selection-screen begin of line.
    selection-screen comment 8(20) tPpath.
    parameters: pFolder like rlgrap-filename memory id MFOLDER.
  selection-screen end of line.
selection-screen: end of block b4.

* Display options
selection-screen: begin of block b5 with frame title tBlock5.
* Display final report
  selection-screen begin of line.
    selection-screen comment 1(33) tRep.
    parameters: pRep as checkbox default 'X'.
  selection-screen end of line.
* Display progress messages
  selection-screen begin of line.
    selection-screen comment 1(33) tProMess.
    parameters: pProMess as checkbox default 'X'.
  selection-screen end of line.
selection-screen: end of block b5.

*----------------------------------------------------------------------------------------------------------------------
* Display a directory picker window
*----------------------------------------------------------------------------------------------------------------------
at selection-screen on value-request for pFolder.

data: objFile type ref to cl_gui_frontend_services.
data: pickedFolder type string.
data: initialFolder type string.

  if sy-batch is initial.
    create object objFile.

    if not pFolder is initial.
      initialFolder = pFolder.
    else.
      objFile->get_temp_directory( changing temp_dir = initialFolder
                                   exceptions cntl_error = 1
                                             error_no_gui = 2
                                             not_supported_by_gui = 3 ).
    endif.

    objFile->directory_browse( exporting initial_folder = initialFolder
                               changing selected_folder = pickedFolder
                               exceptions cntl_error = 1
                                          error_no_gui = 2
                                          not_supported_by_gui = 3 ).

    if sy-subrc = 0.
      pFolder = pickedFolder.
    else.
      write: / 'An error has occured picking a folder'.
    endif.
  endif.

*----------------------------------------------------------------------------------------------------------------------
at selection-screen.
*----------------------------------------------------------------------------------------------------------------------
  case 'X'.
    when pPc.
      if pFolder is initial.
*       User must enter a path to save to
        message e000(oo) with 'You must enter a file path'.
      endif.

    when pServ.
      if pLogical is initial.
*       User must enter a logical path to save to
        message e000(oo) with 'You must enter a logical file name'.
      endif.
  endcase.

*----------------------------------------------------------------------------------------------------------------------
at selection-screen on pLogical.
*----------------------------------------------------------------------------------------------------------------------
  if not pServ is initial.
    call function 'FILE_GET_NAME' exporting logical_filename = pLogical
                                  importing file_name = serverFolder
                                  exceptions file_not_found = 1
                                             others = 2.
    if sy-subrc = 0.
      if serverFolder is initial.
        message e000(oo) with 'No file path returned from logical filename'.
      else.
*       Path to display on the selection screen
        tSDPath = serverFolder.
*       Remove the trailing slash off the path as the subroutine buildFilename will add an extra one
        shift serverFolder right deleting trailing serverSlashSeparator.
        shift serverFolder left deleting leading space.
      endif.
    else.
      message e000(oo) with 'Logical filename does not exist'.
    endif.
  endif.

* ---------------------------------------------------------------------------------------------------------------------
at selection-screen on value-request for soProg-low.
* ---------------------------------------------------------------------------------------------------------------------
  call function 'REPOSITORY_INFO_SYSTEM_F4' exporting object_type  = 'PROG'
                                                      object_name  = soProg-low
                                                      suppress_selection   = 'X'
                                                      use_alv_grid = ''
                                                      without_personal_list = ''
                                            importing object_name_selected = soProg-low
                                            exceptions cancel = 1.

* ---------------------------------------------------------------------------------------------------------------------
at selection-screen on value-request for soProg-high.
* ---------------------------------------------------------------------------------------------------------------------
  call function 'REPOSITORY_INFO_SYSTEM_F4' exporting object_type  = 'PROG'
                                                      object_name  = soProg-high
                                                      suppress_selection   = 'X'
                                                      use_alv_grid = ''
                                                      without_personal_list = ''
                                            importing object_name_selected = soProg-high
                                            exceptions cancel = 1.

* ---------------------------------------------------------------------------------------------------------------------
at selection-screen on value-request for soClass-low.
* ---------------------------------------------------------------------------------------------------------------------
  call function 'F4_DD_ALLTYPES' exporting object = soClass-low
                                           suppress_selection = 'X'
                                           display_only = ''
                                           only_types_for_clifs = 'X'
                                 importing result = soClass-low.

* ---------------------------------------------------------------------------------------------------------------------
at selection-screen on value-request for soClass-high.
* ---------------------------------------------------------------------------------------------------------------------
  call function 'F4_DD_ALLTYPES' exporting object = soClass-high
                                           suppress_selection = 'X'
                                           display_only = ''
                                           only_types_for_clifs = 'X'
                                 importing result = soClass-high.

* ---------------------------------------------------------------------------------------------------------------------
at selection-screen on value-request for soFName-low.
* ---------------------------------------------------------------------------------------------------------------------
  call function 'REPOSITORY_INFO_SYSTEM_F4' exporting object_type  = 'FUNC'
                                                      object_name  = soFname-low
                                                      suppress_selection   = 'X'
                                                      use_alv_grid = ''
                                                      without_personal_list = ''
                                            importing object_name_selected = soFName-low
                                            exceptions cancel = 1.

* ---------------------------------------------------------------------------------------------------------------------
at selection-screen on value-request for soFName-high.
* ---------------------------------------------------------------------------------------------------------------------
  call function 'REPOSITORY_INFO_SYSTEM_F4' exporting object_type  = 'FUNC'
                                                      object_name  = soFname-high
                                                      suppress_selection   = 'X'
                                                      use_alv_grid = ''
                                                      without_personal_list = ''
                                            importing object_name_selected = soFName-high
                                            exceptions cancel = 1.

* ---------------------------------------------------------------------------------------------------------------------
at selection-screen on value-request for soFGroup-low.
* ---------------------------------------------------------------------------------------------------------------------
  call function 'REPOSITORY_INFO_SYSTEM_F4' exporting object_type  = 'FUGR'
                                                      object_name  = soFGroup-low
                                                      suppress_selection   = 'X'
                                                      use_alv_grid = ''
                                                      without_personal_list = ''
                                            importing object_name_selected = soFGroup-low
                                            exceptions cancel = 1.

* ---------------------------------------------------------------------------------------------------------------------
at selection-screen on value-request for soFGroup-high.
* ---------------------------------------------------------------------------------------------------------------------
  call function 'REPOSITORY_INFO_SYSTEM_F4' exporting object_type  = 'FUGR'
                                                      object_name  = soFGroup-high
                                                      suppress_selection   = 'X'
                                                      use_alv_grid = ''
                                                      without_personal_list = ''
                                            importing object_name_selected = soFGroup-high
                                            exceptions cancel = 1.

*----------------------------------------------------------------------------------------------------------------------
* initialisation
*----------------------------------------------------------------------------------------------------------------------
initialization.
* Parameter screen texts.
  tBlock1 = 'Author (Optional)'.
  tBlock2 = 'Objects to download'.
  tBlock3 = 'Additional downloads for programs, function modules and classes'.
  tBlock4 = 'Download parameters'.
  tBlock5 = 'Display options'.
  tAuth   = 'Author name'.
  tPmod   = 'Include programs modified by author'.
  tCust   = 'Only customer objects'.
  tNRange = 'Alt customer name range'.
  tRtable = 'Tables / Structures'.
  tPtable = 'Table name'.
  tTnote  = 'Note: tables are stored under the username of the last person who modified them'.
  tRfunc  = 'Function modules'.
  tPfname = 'Function name'.
  tFgroup = 'Function group'.
  tRClass  = 'Classes'.
  tPcname = 'Class name'.
  tMess   = 'Message class'.
  tMName  = 'Class name'.
  tMLang  = 'Language'.
  tProg   = 'Programs'.
  tRpname = 'Program name'.
  tPack   = 'Package'.
  tPtxt   = 'Text document'.
  tPhtml  = 'HTML document'.
  tBack   = 'Include background colour'.
  tPtext  = 'Text elements'.
  tPinc   = 'Include programs'.
  tRecI   = 'Recursive search'.
  tPpath  = 'File path'.
  tSPath  = 'Logical file name'.
  tPmes   = 'Message classes'.
  tPfunc  = 'Function modules'.
  tFDoc    = 'Function module documentation'.
  tCDoc    = 'Class documentation'.
  tRecf   = 'Recursive search'.
  tRecC   = 'Class recursive search'.
  tPscr   = 'Screens'.
  tPdict  = 'Dictionary structures'.
  tSortT  = 'Sort table fields alphabetically'.
  tServ   = 'Download to server'.
  tPc     = 'Download to PC'.
  tRep    = 'Display download report'.
  tProMess  = 'Display progress messages'.

* Determine the frontend operating system type.
  if sy-batch is initial.
    perform determineFrontendOPSystem using frontendSlashSeparator frontendOpSystem.
  endif.
  perform determineServerOpsystem using serverSlashSeparator serverFileSystem serverOpsystem.

* Determine if the external command exists.  If it doesn't then disable the server input field
  perform findExternalCommand using serverFileSystem.


*----------------------------------------------------------------------------------------------------------------------
start-of-selection.
*----------------------------------------------------------------------------------------------------------------------
  perform checkComboBoxes.
  perform fillSelectionRanges.
  startTime = sy-uzeit.

* Don't display status messages if we are running in the background
  if not sy-batch is initial.
    pProMess = ''.
  endif.

* Fool the HTML routines to stop them hyperlinking anything with a space in them
  if pCName is initial.
    customerNameSpace  = '^'.
  else.
    customerNameSpace = pCName.
  endif.

* Set the file extension and output type of the file
  if pTxt is initial.
    downloadFileExtension = HTMLEXTENSION.
  else.
    downloadFileExtension = TEXTEXTENSION.
  endif.

* Determine which operating slash and download directory to use
  case 'X'.
    when pPc.
      slashSeparatorToUse = frontendSlashSeparator.
      downloadFolder = pFolder.
    when pServ.
      slashSeparatorToUse = serverSlashSeparator.
      downloadFolder = serverFolder.
  endcase.

* Main program flow.
  case 'X'.
*   Select tables
    when rTable.
      perform retrieveTables using iDictionary[]
                                   soTableNames[]
                                   soAuthor[]
                                   soPack[].

*   Select message classes tables
    when rMess.
      perform retrieveMessageClass using iMessages[]
                                         soAuthor[]      "Author
                                         pMname          "Message class name
                                         pMLang          "Message class language
                                         pMod            "Modified by author
                                         soPack[].       "Package

*   Select function modules
    when rFunc.
      perform retrieveFunctions using soFunctionName[]   "Function name
                                      soFunctionGroup[]  "Function group
                                      iFunctions[]       "Found functions
                                      soAuthor[]         "Author
                                      pText              "Get text elements
                                      pScr               "Get screens
                                      pCust              "Customer data only
                                      customerNameSpace  "Customer name range
                                      soPack[].             "Package

*       Find Dict structures, messages, functions, includes etc.
        perform scanForAdditionalFuncStuff using iFunctions[]
                                                 pRecI                   "Search for includes recursively
                                                 pRecF                   "Search for functions recursively
                                                 pInc                    "Search for includes
                                                 pFunc                   "Search for functions
                                                 pDict                   "search for dictionary objects
                                                 pMess                   "Search for messages
                                                 pCust                   "Customer data only
                                                 customerNameSpace.      "Customer name range

*   Select Classes
    when rClass.
      perform retrieveClasses using iClasses[]
                                    iFunctions[]
                                    soClassName[]       "Class name
                                    soAuthor[]          "Author
                                    customerNameSpace   "Customer name range
                                    pMod                "Also modified by author
                                    pCust               "Customer object only
                                    pMess               "Find messages
                                    pText               "Text Elements
                                    pDict               "Dictionary structures
                                    pFunc               "Get functions
                                    pInc                "Get includes
                                    pRecF               "Search recursively for functions
                                    pRecI               "Search recursively for includes
                                    pRecC               "Search recursively for classes
                                    pMLang              "Language
                                    soPack[].           "Package

      loop at iFunctions.
*       Find Dict structures, messages, functions, includes etc.
        perform scanForAdditionalFuncStuff using iFunctions[]
                                                 pRecI                   "Search for includes recursively
                                                 pRecF                   "Search for functions recursively
                                                 pInc                    "Search for includes
                                                 pFunc                   "Search for functions
                                                 pDict                   "search for dictionary objects
                                                 pMess                   "Search for messages
                                                 pCust                   "Customer data only
                                                 customerNameSpace.      "Customer name range
      endloop.


*   Select programs
    when rProg.
      perform retrievePrograms using iPrograms[]
                                     iProgFunctions[]
                                     soProgramName[]    "Program name
                                     soAuthor[]         "Author
                                     customerNamespace  "Customer name range
                                     pMod               "Also modified by author
                                     pCust              "Customer object only
                                     pMess              "Find messages
                                     pText              "Text Elements
                                     pDict              "Dictionay structures
                                     pFunc              "Get functions
                                     pInc               "Get includes
                                     pScr               "Get screens
                                     pRecF              "Search recursively for functions
                                     pRecI              "Search recursively for includes
                                     soPack[].             "Package
  endcase.

*----------------------------------------------------------------------------------------------------------------------
end-of-selection.
*----------------------------------------------------------------------------------------------------------------------
  if forcedExit = 0.
*   Decide what to download
    case 'X'.
*     Download tables
      when rTable.
        if not ( iDictionary[] is initial ).
          perform downloadDDStructures using iDictionary[]
                                             iDictFilename[]
                                             downloadFolder
                                             HTMLEXtension
                                             space
                                             pSortT
                                             slashSeparatorToUse
                                             pServ
                                             pProMess
                                             serverFileSystem
                                             pBack.
        endif.

*     Download message class
      when rMess.
        if not ( iMessages[] is initial ).
          sort iMessages ascending by arbgb msgnr.
          loop at iMessages.
            append iMessages to iSingleMessageClass.
            at end of arbgb.
              perform downloadMessageClass using iSingleMessageClass[]
                                                 iMessages-arbgb
                                                 downloadFolder
                                                 downloadFileExtension
                                                 pHtml
                                                 space
                                                 customerNameSpace
                                                 pInc
                                                 pDict
                                                 pMess
                                                 slashSeparatorToUse
                                                 pServ
                                                 pProMess
                                                 serverFileSystem
                                                 pBack.
              clear iSingleMessageClass[].
            endat.
          endloop.
       endif.

*     Download functions
      when rFunc.
        if not ( iFunctions[] is initial ).
           perform downloadFunctions using iFunctions[]
                                           iDictFilename[]
                                           downloadFolder
                                           downloadFileExtension
                                           space
                                           pFDoc
                                           pHtml
                                           customerNameSpace
                                           pInc
                                           pDict
                                           TEXTEXTENSION
                                           HTMLEXTENSION
                                           pSortT
                                           slashSeparatorToUse
                                           pServ
                                           pProMess
                                           serverFileSystem
                                           pBack.
        endif.

*     Download Classes
      when rClass.
        if not ( iClasses[] is initial ).
          perform downloadClasses using iClasses[]
                                        iFunctions[]
                                        iDictFilename[]
                                        downloadFolder
                                        downloadFileExtension
                                        HTMLEXTENSION
                                        TEXTEXTENSION
                                        pHtml
                                        customerNameSpace
                                        pInc
                                        pDict
                                        pCDoc
                                        pSortT
                                        slashSeparatorToUse
                                        pServ
                                        pProMess
                                        serverFileSystem
                                        pBack.
        endif.

*     Download programs
      when rProg.
        if not ( iPrograms[] is initial ).
          perform downloadPrograms using iPrograms[]
                                         iProgFunctions[]
                                         iDictFilename[]
                                         downloadFolder
                                         downloadFileExtension
                                         HTMLEXTENSION
                                         TEXTEXTENSION
                                         pHtml
                                         customerNameSpace
                                         pInc
                                         pDict
                                         '' "Documentation
                                         pSortT
                                         slashSeparatorToUse
                                         pServ
                                         pProMess
                                         serverFileSystem
                                         pBack.
          endif.
    endcase.


*   Free all the memory IDs we may have built up in the program
*   Free up any memory used for caching HTML versions of objects
    perform freeMemory using iPrograms[]
                             iFunctions[]
                             iProgFunctions[]
                             iDictionary[].

    if not pRep is initial.
      get time.
      runTime = sy-uzeit - startTime.

      case 'X'.
        when rTable.
          perform fillTreeNodeTables using iDictionary[]
                                           iTreeDisplay[]
                                           runTime.

        when rMess.
          perform fillTreeNodeMessages using iMessages[]
                                             iTreeDisplay[]
                                             runTime.


        when rFunc.
          perform fillTreeNodeFunctions using iFunctions[]
                                              iTreeDisplay[]
                                              runTime.

        when rClass.
          perform fillTreeNodeClasses using iClasses[]
                                            iFunctions[]
                                            iTreeDisplay[]
                                            runTime.

        when rProg.
          perform fillTreeNodePrograms using iPrograms[]
                                             iProgFunctions[]
                                             iTreeDisplay[]
                                             runTime.
      endcase.

      if not ( iTreeDisplay[] is initial ).
        perform displayTree using iTreeDisplay[].
      else.
        statusBarMessage = 'No items found matching selection criteria'.
        perform displayStatus using statusBarMessage 2.
      endif.
    endif.
  endif.

* Clear out all the internal tables
  clear iPrograms[].
  clear iFunctions[].
  clear iClasses[].
  clear iProgFunctions[].
  clear iMessages[].
  clear iDictionary[].
  clear iDictFilename[].

*--- Memory IDs
* User name
  set parameter id 'MAUTH' field pAuth.
* Message class
  set parameter id 'MMNAME' field pMname.
* Customer namespace
  set parameter id 'MNAMESPACE' field pCName.
* Folder
  set parameter id 'MFOLDER' field pFolder.
* Logical filepath
  set parameter id 'MLOGICAL' field pLogical.
* Text element checkbox
  set parameter id 'MTEXT' field pText.
* Messages checkbox
  set parameter id 'MMESS' field pMess.
* Includes checkbox
  set parameter id 'MINC' field pInc.
* Recursive includes checkbox.
  set parameter id 'MRECI' field pReci.
* Functions checkbox
  set parameter id 'MFUNC' field pFunc.
* Recursive functions checkbox
  set parameter id 'MRECF' field pRecf.
* Recursive classes checkbox
  set parameter id 'MRECF' field pRecC.
* Function module documentation checkbox
  set parameter id 'MFDOC' field pFDoc.
* Class documentation checkbox
  set parameter id 'MCDOC' field pCDoc.
* Screens checkbox
  set parameter id 'MSCR' field pScr.
* Dictionary checkbox
  set parameter id 'MDICT' field pDict.
* Sort table ascending checkBox
  set parameter id 'MSORTT' field pSortT.


***********************************************************************************************************************
***************************************************SUBROUTINES*********************************************************
***********************************************************************************************************************

*----------------------------------------------------------------------------------------------------------------------
*  free memory...
*----------------------------------------------------------------------------------------------------------------------
form freeMemory using iLocPrograms like iPrograms[]
                      iLocFunctions like iFunctions[]
                      iLocProgfunctions like iProgFunctions[]
                      iLocDictionary like iDictionary[].

field-symbols: <wafunction> like line of iLocfunctions.
field-symbols: <waProgram> like line of iLocPrograms.
field-symbols: <waDictStruct> type tDictTable.

  loop at iLocFunctions assigning <waFunction>.
    loop at <waFunction>-iDictStruct assigning <waDictStruct>.
      free memory id <waDictStruct>-tablename.
    endloop.
  endloop.

  loop at iLocProgFunctions assigning <waFunction>.
    loop at <waFunction>-iDictStruct assigning <waDictStruct>.
      free memory id <waDictStruct>-tablename.
    endloop.
  endloop.

  loop at iLocPrograms assigning <waProgram>.
    loop at <waProgram>-iDictStruct assigning <waDictStruct>.
      free memory id <waDictStruct>-tablename.
    endloop.
  endloop.

  loop at iLocDictionary assigning <waDictStruct>.
    free memory id <waDictStruct>-tablename.
  endloop.
endform.


*----------------------------------------------------------------------------------------------------------------------
*  checkComboBoxes...  Check input parameters
*----------------------------------------------------------------------------------------------------------------------
form checkComboBoxes.

  if pAuth is initial.
    if soPack[] is initial.
      case 'X'.
        when rTable.
          if soTable[] is initial.
            statusBarMessage = 'You must enter either a table name or author.'.
          endif.
        when rFunc.
          if ( soFName[] is initial ) and ( soFGroup[] is initial ).
            if soFName[] is initial.
              statusBarMessage = 'You must enter either a function name or author.'.
            else.
              if soFGroup[] is initial.
                statusBarMessage = 'You must enter either a function group, or an author name.'.
              endif.
            endif.
          endif.
        when rProg.
          if soProg[] is initial.
              statusBarMessage = 'You must enter either a program name or author name.'.
          endif.
      endcase.
    endif.
  else.
*   Check the user name of the person objects are to be downloaded for
    if pAuth = 'SAP*' or pauth = 'SAP'.
      statusBarMessage = 'Sorry cannot download all objects for SAP standard user'.
    endif.
  endif.

  if not statusBarMessage is initial.
    perform displayStatus using statusBarMessage 3.
    forcedExit = 1.
    stop.
  endif.
endform.                                                                                "checkComboBoxes

*----------------------------------------------------------------------------------------------------------------------
* fillSelectionRanges...      for selection routines
*----------------------------------------------------------------------------------------------------------------------
form fillSelectionRanges.

data: strLength type i.

  strLength = strlen( pcName ).

  if not pAuth is initial.
    soAuthor-sign = 'I'.
    soAuthor-option = 'EQ'.
    soAuthor-low = pAuth.
    append soAuthor.
  endif.

* Tables
  if not soTable is initial.
    soTableNames[] = soTable[].
*   Add in the customer namespace if we need to
    if not pcName is initial.
       loop at soTableNames.
        if soTableNames-low+0(strLength) <> pcName.
          concatenate pcName soTableNames-low into soTableNames-low.
        endif.

        if soTableNames-high+0(strLength) <> pcName.
          concatenate pcName soTableNames-high into soTableNames-high.
        endif.

        modify soTableNames.
      endloop.
    endif.
  endif.

* Function names
  if not soFName is initial.
    soFunctionName[] = soFname[].
*   Add in the customer namespace if we need to
    if not pcName is initial.
       loop at soFunctionName.
        if soFunctionName-low+0(strLength) <> pcName.
          concatenate pcName soFunctionName-low into soFunctionName-low.
        endif.

        if soFunctionName-high+0(strLength) <> pcName.
          concatenate pcName soFunctionName-high into soFunctionName-high.
        endif.

        modify soFunctionName.
      endloop.
    endif.
  endif.

* Function group
  if not soFGroup is initial.
    soFunctionGroup[] = soFGroup[].
*   Add in the customer namespace if we need to
    if not pcName is initial.
       loop at soFunctionName.
        if soFunctionGroup-low+0(strLength) <> pcName.
          concatenate pcName soFunctionGroup-low into soFunctionGroup-low.
        endif.

        if soFunctionGroup-high+0(strLength) <> pcName.
          concatenate pcName soFunctionGroup-high into soFunctionGroup-high.
        endif.

        modify soFunctionGroup.
      endloop.
    endif.
  endif.

* Class names
  if not soClass is initial.
    soClassName[] = soClass[].
*   Add in the customer namespace if we need to
    if not pcName is initial.
       loop at soClassName.
        if soClassName-low+0(strLength) <> pcName.
          concatenate pcName soClassName-low into soClassName-low.
        endif.

        if soClassName-high+0(strLength) <> pcName.
          concatenate pcName soClassName-high into soClassName-high.
        endif.

        modify soClassName.
      endloop.
    endif.
  endif.

* Program names
  if not soProg is initial.
    soProgramName[] = soProg[].
*   Add in the customer namespace if we need to
    if not pcName is initial.
       loop at soProgramName.
        if soProgramName-low+0(strLength) <> pcName.
          concatenate pcName soProgramName-low into soProgramName-low.
        endif.

        if soProgramName-high+0(strLength) <> pcName.
          concatenate pcName soProgramName-high into soProgramName-high.
        endif.

        modify soProgramName.
      endloop.
    endif.
  endif.
endform.                                                                                          " fillSelectionRanges

*----------------------------------------------------------------------------------------------------------------------
*  retrieveTables...             Search for tables in dictionary
*----------------------------------------------------------------------------------------------------------------------
form retrieveTables using iLocDictStructure like iDictionary[]
                          soTable like soTable[]
                          soAuthor like soAuthor[]
                          value(soLocPackage) like soPack[].

data: iDictStructure type standard table of tDictTable.
data: waDictStructure type tDictTable.

  select a~tabname as tableName
         into corresponding fields of table iDictStructure
         from dd02l as a
         inner join tadir as b
           on a~tabname = b~obj_name
         where a~tabname in soTable
           and a~tabclass <> 'CLUSTER'
           and a~tabclass <> 'POOL'
           and a~tabclass <> 'VIEW'
           and a~as4user in soAuthor
           and a~as4local = 'A'
           and b~pgmid = 'R3TR'
           and b~object = 'TABL'
           and b~devclass in soLocPackage[].

  loop at iDictStructure into waDictStructure.
    perform findTableDescription using waDictStructure-tablename
                                       waDictStructure-tableTitle.

    perform findTableDefinition using waDictStructure-tableName
                                      waDictStructure-iStructure[].

    append waDictStructure to iLocDictStructure.
    clear waDictStructure.
  endloop.
endform.                                                                                                "retrieveTables

*----------------------------------------------------------------------------------------------------------------------
*  findTableDescription...  Search for table description in dictionary
*----------------------------------------------------------------------------------------------------------------------
form findTableDescription using value(tableName)
                                      tableDescription.

    select single ddtext
                  from dd02t
                  into tableDescription
                  where tabname = tableName
                   and ddlanguage = pMLang.
endform.                                                                                          "findTableDescription

*----------------------------------------------------------------------------------------------------------------------
*  findTableDefinition... Find the structure of a table from the SAP database.
*----------------------------------------------------------------------------------------------------------------------
form findTableDefinition using value(tablename)
                               iDictStruct like dumIDictStructure[].

data gotstate like dcobjif-gotstate.
data: definition type standard table of DD03P with header line.
data: waDictStruct type tDictTableStructure.

  call function 'DDIF_TABL_GET'
       exporting
            name          = tablename
            state         = 'A'
            langu         = pMLang
       importing
            gotstate      = gotstate
       tables
            dd03p_tab     = definition
       exceptions
            illegal_input = 1
            others        = 2.

  if sy-subrc = 0 and gotstate = 'A'.
    loop at definition.
      move-corresponding definition to waDictStruct.
      perform removeLeadingZeros changing waDictStruct-position.
      perform removeLeadingZeros changing waDictStruct-leng.
      append waDictStruct to iDictStruct.
    endloop.
  endif.
endform.                                                                                           "findTableDefinition

*----------------------------------------------------------------------------------------------------------------------
*  retrieveMessageClass...   Retrieve a message class from the SAP database
*----------------------------------------------------------------------------------------------------------------------
form retrieveMessageClass using iLocMessages like iMessages[]
                                rangeAuthor like soAuthor[]
                                value(messageClassName)
                                value(messageClassLang)
                                value(modifiedBy)
                                value(soLocPackage) like soPack[].

data: waMessage type tMessage.
data: iMClasses type standard table of arbgb.


  if not messageClassName is initial.
*   Check to see if the message class exists in the package
    if not soLocPackage[] is initial.
        select obj_name as arbgb
               into table iMClasses
               from tadir
               where pgmid = 'R3TR'
                 and object = 'MSAG'
                 and devclass in soLocPackage.
    endif.

    replace '*' with '%' into messageClassName.
    if iMCLasses[] is initial.
      select t100~arbgb
             t100~text
             t100~msgnr
             t100a~stext
             appending corresponding fields of table iLocMessages
             from t100
             inner join t100a
               on t100a~arbgb = t100~arbgb
             where t100a~masterLang = messageClassLang
               and t100~sprsl = messageClassLang
               and t100~arbgb like messageClassName
               and t100a~respUser in rangeAuthor[].
    else.
      select t100~arbgb
             t100~text
             t100~msgnr
             t100a~stext
             appending corresponding fields of table iLocMessages
             from t100
             inner join t100a
               on t100a~arbgb = t100~arbgb
             for all entries in iMClasses
               where t100~sprsl = messageClassLang
               and ( t100~arbgb like messageClassName and t100~arbgb = iMClasses-table_line )
               and t100a~masterLang = messageClassLang
               and t100a~respUser in rangeAuthor[].
    endif.
  else.
    if modifiedBy is initial.
*       Select by author
        select t100~arbgb                             "#EC CI_BUFFJOIN
               t100~msgnr
               t100~text
               t100a~stext
               appending corresponding fields of table iLocMessages
               from t100
               inner join t100a
                 on t100a~arbgb = t100~arbgb
               inner join tadir
                 on t100~arbgb = tadir~obj_name
               where t100a~masterLang = messageClassLang
                 and t100a~respUser in rangeAuthor[]
                 and tadir~pgmid = 'R3TR'
                 and tadir~object = 'MSAG'
                 and tadir~devclass in soLocPackage[].

    else.
*     Select also by the last person who modified the message class
      select t100~arbgb                             "#EC CI_BUFFJOIN
             t100~msgnr
             t100~text
             t100a~stext
             appending corresponding fields of table iLocMessages
             from t100
             inner join t100a
               on t100a~arbgb = t100~arbgb
             inner join tadir
               on t100~arbgb = tadir~obj_name
             where t100a~masterLang = messageClassLang
               and t100a~respUser in rangeAuthor[]
               and t100a~lastUser in rangeAuthor[]
               and tadir~pgmid = 'R3TR'
               and tadir~object = 'MSAG'
               and tadir~devclass in soLocPackage[].
    endif.
  endif.
endform.                                                                                          "retrieveMessageClass


*----------------------------------------------------------------------------------------------------------------------
*  retrieveFunctions...   Retrieve function modules from SAP DB.  May be called in one of two ways
*----------------------------------------------------------------------------------------------------------------------
form retrieveFunctions using soFName like soFunctionName[]
                             soFGroup like soFunctionGroup[]
                             iLocFunctionNames like iFunctions[]
                             value(solocAuthor) like soAuthor[]
                             value(getTextElements)
                             value(getScreens)
                             value(customerOnly)
                             value(customerNameRange)
                             value(soLocPackage) like soPack[].


data: waFunctionName type tFunction.
data: noGroupsFound type abap_bool value TRUE.
data: previousFG type v_fdir-area.

* select by function name and/or function group.
  select a~funcName as functionName
         a~area as functionGroup
         into corresponding fields of table iLocfunctionNames
         from v_fdir as a
         inner join tlibv as b
           on a~area = b~area
         inner join tadir as c
           on a~area = c~obj_name
         where a~funcName in soFName[]
           and a~area in soFGroup[]
           and a~generated = ''
           and b~uname in soLocAuthor[]
           and pgmid = 'R3TR'
           and object = 'FUGR'
           and devclass in solocPackage[]
           order by a~area.

  loop at iLocFunctionNames into waFunctionName.
    perform retrieveFunctionDetail using waFunctionName-functionName
                                         waFunctionName-progname
                                         waFunctionName-includeNumber
                                         waFunctionName-functionTitle.

    perform findMainFunctionInclude using waFunctionName-progname
                                          wafunctionName-functionGroup
                                          waFunctionName-includeNumber
                                          waFunctionName-functionMainInclude.

    perform findFunctionTopInclude using waFunctionName-progname
                                         wafunctionName-functionGroup
                                         waFunctionName-topIncludeName.

*   Find all user defined includes within the function group
    perform scanForFunctionIncludes using waFunctionName-progname
                                          customerOnly
                                          customerNameRange
                                          waFunctionName-iIncludes[].
*   Find main message class
    perform findMainMessageClass using waFunctionName-progname
                                       waFunctionName-messageClass.

*   Find any screens declared within the main include
    if not getScreens is initial.
      if previousFG is initial or previousFG <> waFunctionName-functionGroup.
        perform findFunctionScreenFlow using waFunctionName.

*       Search for any GUI texts
        perform retrieveGUITitles using waFunctionName-iGUITitle[]
                                        waFunctionName-progname.
      endif.
    endif.

    if not getTextElements is initial.
*     Find the program texts from out of the database.
      perform retrieveProgramTexts using waFunctionName-iSelectionTexts[]
                                         waFunctionName-iTextElements[]
                                         waFunctionName-progname.
    endif.

    previousFG = waFunctionName-functionGroup.
    modify iLocFunctionNames from waFunctionName.
  endloop.
endform.                                                                                             "retrieveFunctions

*----------------------------------------------------------------------------------------------------------------------
*  retrieveFunctionDetail...   Retrieve function module details from SAP DB.
*----------------------------------------------------------------------------------------------------------------------
form retrieveFunctionDetail using value(functionName)
                                        progname
                                        includeName
                                        titleText.

  select single pname
                include
                from tfdir
                into (progname, includeName)
                where funcName = functionName.

  if sy-subrc = 0.
    select single stext
                  from tftit
                  into titleText
                  where spras = pMLang
                    and funcName = functionName.
  endif.
endform.                                                                                        "retrieveFunctionDetail                                                                                  "findFunctionTopInclude

*----------------------------------------------------------------------------------------------------------------------
* scanForAdditionalFuncStuff... Search for additional things relating to functions
*----------------------------------------------------------------------------------------------------------------------
form scanForAdditionalFuncStuff using iLocFunctions like iFunctions[]
                                      value(recursiveIncludes)
                                      value(recursiveFunctions)
                                      value(searchForIncludes)
                                      value(searchForFunctions)
                                      value(searchForDictionary)
                                      value(searchForMessages)
                                      value(customerOnly)
                                      value(customerNameRange).

data: waFunction type tFunction.
data: waInclude type tInclude.

  loop at iLocFunctions into waFunction.
    if not searchForIncludes is initial.
*     Search in the main include
      perform scanForIncludePrograms using waFunction-functionMainInclude
                                           recursiveIncludes
                                           customerOnly
                                           customerNameRange
                                           waFunction-iIncludes[].

*     Search in the top include
      perform scanForIncludePrograms using waFunction-topIncludeName
                                           recursiveIncludes
                                           customerOnly
                                           customerNameRange
                                           waFunction-iIncludes[].
    endif.



